input _MbEvent&& MB_REMOVED;
input _MbEvent&& MB_END;
input _MbEvent&& MB_APP_INIT_DONE;

native @nohold _printf (),            \
               _strcmp (),            \
               _mb_media_free (),     \
               _mb_media_set_pos (),  \
               _mb_media_set_size (), \
               _mb_media_stop (),     \
               _mb_media_start ();

#define FALSE 0 

var int width = 640;
var int height = 480;

_mb_init_args (width, height, FALSE);
await MB_APP_INIT_DONE;

var _MbMedia&? background;
var _MbMedia&? anim;
var _MbMedia&? choro;
var _MbMedia&? drible;
var _MbMedia&? shoes;

finalize
  background = &_mb_media_new ("back",
        //one should change the path below
        "file:///media/rodrigocosta/Data/primeirojoao/media/background.png", 
        0, 0, 0, width, height);
with
  _mb_media_free (&&background!);
end
finalize
  anim = & _mb_media_new ("anim",
        //one should change the path below
        "file:///media/rodrigocosta/Data/primeirojoao/media/animGar.mp4", 
        0, 0, 1, width, height);
with
  _mb_media_free (&&anim!);
end
finalize
  choro = & _mb_media_new ("choro",
        //one should change the path below
        "file:///media/rodrigocosta/Data/primeirojoao/media/choro.mp4", 
        0, 0, 0, 0, 0);
with
  _mb_media_free (&&choro!);
end
finalize
  drible = & _mb_media_new ("drible",
        //one should change the path below
        "file:///media/rodrigocosta/Data/primeirojoao/media/drible.mp4", 
        width * 0.05, 
        height * 0.067, 
        3, 
        width * 0.185,
        height * 0.185);
with
  _mb_media_free (&&drible!);
end
finalize
  shoes = & _mb_media_new ("shoes",
        //one should change the path below
        "file:///media/rodrigocosta/Data/primeirojoao/media/shoes.mp4", 
         width * 0.15,
         height * 0.60,
         3,
         width * 0.25,
         height * 0.25);
with
  _mb_media_free (&&shoes!);
end

var int count = 0;

par/or do
  every evt in MB_END do
    if _strcmp (evt:state_change.media_name, shoes!.name) == 0 then
      _mb_media_set_pos (&&anim!, 0, 0);
      _mb_media_set_size (&&anim!, width, height);
    else/if _strcmp (evt:state_change.media_name, anim!.name) == 0 then
        _mb_media_stop (&&background!);
    end
  end
with
  loop do
     await MB_REMOVED;
     count = count + 1; 
     if count == 5 then //# of medias
       break;
     end
  end
with
  _mb_media_start (&&background!);
  _mb_media_start (&&anim!);

  await 5s;
  _mb_media_start (&&choro!);
  
  await 7s;
  _mb_media_start (&&drible!);
    
  await 33s;
  
  _mb_media_set_pos (&&anim!, width * 0.05, height * 0.067);
  _mb_media_set_size (&&anim!, width * 0.45, height * 0.45);

  _mb_media_start (&&shoes!);

  await FOREVER;
end

escape 0;
